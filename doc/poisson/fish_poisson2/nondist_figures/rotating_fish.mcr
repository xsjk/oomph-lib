#!MC 1000

$!DRAWGRAPHICS FALSE


$!VARSET |NORIG|=|NUMZONES|

$!LOOP |NORIG|
 $!DUPLICATEZONE
   SOURCEZONE = |LOOP|
$!ENDLOOP

$!ALTERDATA  [1-|NORIG|]
  EQUATION = 'v3=-v3' 


$!THREEDAXIS XDETAIL{SHOWAXIS = NO}
$!THREEDAXIS YDETAIL{SHOWAXIS = NO}
$!THREEDAXIS ZDETAIL{SHOWAXIS = NO}

$!FIELDLAYERS SHOWMESH = NO
$!FIELD [1-|NUMZONES|]  BOUNDARY{LINETHICKNESS = 0.02}
$!FIELD [1-|NUMZONES|]  BOUNDARY{COLOR = RED}
$!FIELDLAYERS SHOWSHADE = YES
$!FIELDLAYERS USETRANSLUCENCY = NO


$!THREEDVIEW 
  PSIANGLE = 16.3986
  THETAANGLE = 172.611
  ALPHAANGLE = -171.016
  VIEWERPOSITION
    {
    X = -0.112074276045
    Y = 5.79527430638
    Z = 3.4665713338
    }


$!DRAWGRAPHICS TRUE
$!VIEW PUSH
$!VIEW FIT

$!EXPORTSETUP EXPORTFORMAT = AVI
$!EXPORTSETUP IMAGEWIDTH = 798
$!EXPORTSETUP EXPORTFNAME = 'rotating_fish.avi'
$!EXPORTSTART 

$!VARSET |NSTEPS|=10
$!VARSET |DELTA_PHI|=360
$!VARSET |DELTA_PHI|/=|NSTEPS|


$!LOOP |NSTEPS|
        $!ROTATE3DVIEW Y
          ANGLE = |DELTA_PHI|
          ROTATEORIGINLOCATION = DEFINEDORIGIN
        $!EXPORTNEXTFRAME
$!ENDLOOP


$!EXPORTFINISH 

