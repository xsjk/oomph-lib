#------------------------------------------------------------
# Solution in inner region with given boundary temperature U0
#------------------------------------------------------------
ode0:=Beta0*(diff(u0(r),r$2)+1/r*diff(u0(r),r))-S0;
u0_test(r):=S0/(4*Beta0)*(r^2-Radius_innermost^2)+U0;
test:=eval(subs(u0(r)=u0_test(r),ode0));
u0(r):=u0_test(r);

#------------------------------------------------------------
# Net influx of energy at boundary of inner region
# Positive slope du/dr>0 means temperature is lower
# inside so flux of heat goes INTO the inner region
#------------------------------------------------------------
influx0:=subs(r=Radius_innermost,Beta0*diff(u0_test(r),r));

#------------------------------------------------------------
# Influx generated by difference between positive 
# radiation from outer region (view factor 1)
# and radiation away from heated inner surface
#------------------------------------------------------------
flux_eqn0:=Sigma*((Theta_0+U1)^4-(Theta_0+U0)^4)-influx0; 


#------------------------------------------------------------
# Solve this for U1
#------------------------------------------------------------
U1_test:=((influx0/Sigma+(Theta_0+U0)^4))^(1/4)-Theta_0;
test:=simplify(subs(U1=U1_test,flux_eqn0));
U1:=U1_test;

#---------------------------------------------------------------------
# Solution in outer region with given boundary temperatures U1 and U2
#---------------------------------------------------------------------
ode1:=Beta1*(diff(u1(r),r$2)+1/r*diff(u1(r),r))-S1;
soln:=dsolve({ode1,u1(Radius_outer)=U2,u1(Radius_inner)=U1},u1(r));
assign(soln);


#------------------------------------------------------------
# Net influx of energy at inner boundary of outer region
# Positive slope (du/dr>0 means that there outflux of
# heat from the outer region. 
#------------------------------------------------------------
influx1:=-Beta1*subs(r=Radius_inner,diff(u1(r),r));


#--------------------------------------------------------------------
# Influx generated by difference between positive 
# radiation from inner region (view factor Radius_innermost/Radius_inner)
# and radiation away from heated outer surface (view factor 1-Radius_innermost/Radius_inner)
#--------------------------------------------------------------------
flux_eqn1:=Sigma*((Theta_0+U0)^4*Radius_innermost/Radius_inner-(Theta_0+U1)^4*(1-(Radius_innermost/Radius_inner)))-influx1; 
U2_result:=solve(flux_eqn1,U2);
U2:=U2_result;



#--------------------------------------------------------------------
# Final check
#--------------------------------------------------------------------
test1:=simplify(eval(ode0));
test2:=simplify(eval(ode1));
test3:=simplify(eval(flux_eqn0));
test4:=simplify(eval(flux_eqn1));



#--------------------------------------------------------------------
# Output solutions in C/C++ format 
#--------------------------------------------------------------------
with(codegen);
C(evalf(u0(r)));
C(evalf(u1(r)));

