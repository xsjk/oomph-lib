#!MC 1100

# Use png output (otherwise avi)
$!VARSET |PNG|=0

#$!VARSET |dir|='RESLT' #'_Re50_fine' # 
$!VARSET |dir|='Validation/RESLT'


$!VARSET |SHOW_MAX_MIN|=0
$!VARSET |u_min|=1000000
$!VARSET |u_max|=-10000000
$!VARSET |v_min|=1000000
$!VARSET |v_max|=-10000000
$!VARSET |p_min|=1000000
$!VARSET |p_max|=-10000000


$!VARSET |nstep|=6 #250
$!VARSET |lostep|=1
$!VARSET |dlstep|=1
$!LOOP |nstep|


$!DRAWGRAPHICS FALSE

$!NEWLAYOUT
$!PAPER SHOWPAPER = YES

$!VARSET |istep|=((|LOOP|-1)*|dlstep|+|lostep|)


$!READDATASET  '"|dir|/soln|istep|.dat" '
  READDATAOPTION = NEW
  RESETSTYLE = YES
  INCLUDETEXT = YES
  INCLUDEGEOM = NO
  INCLUDECUSTOMLABELS = NO
  VARLOADMODE = BYNAME
  ASSIGNSTRANDIDS = YES
  INITIALPLOTTYPE = CARTESIAN2D


$!GLOBALCONTOUR 1  VAR = 5
$!CONTOURLEVELS RESETTONICE
  CONTOURGROUP = 1
  APPROXNUMVALUES = 15
$!FIELDLAYERS SHOWCONTOUR = YES
$!FIELDLAYERS SHOWMESH = NO
$!FIELDMAP [1-|NUMZONES|]  EDGELAYER{LINETHICKNESS = 0.0200000000000000004}
$!GLOBALTWODVECTOR UVAR = 3
$!GLOBALTWODVECTOR VVAR = 4
$!RESETVECTORLENGTH 
$!GLOBALTWODVECTOR RELATIVELENGTH = 0.1
$!FIELDLAYERS SHOWVECTOR = YES
$!FIELDMAP [1-|NUMZONES|]  VECTOR{COLOR = WHITE}
$!TWODAXIS GRIDAREA{DRAWBORDER = YES}
$!TWODAXIS YDETAIL{TITLE{TITLEMODE = USETEXT}}
$!TWODAXIS YDETAIL{TITLE{TEXT = 'y'}}
$!TWODAXIS XDETAIL{TITLE{TITLEMODE = USETEXT}}
$!TWODAXIS XDETAIL{TITLE{TEXT = 'x'}}
$!FIELDMAP [1-|NUMZONES|]  VECTOR{COLOR = BLACK}
$!FIELDMAP [1-|NUMZONES|]  EDGELAYER{COLOR = PURPLE}


$!IF |MAXU|>|u_max| 
  $!VARSET  |u_max|=|MAXU|
$!ENDIF

$!IF |MINU|<|u_min| 
   $!VARSET |u_min|=|MINU|
$!ENDIF


$!IF |MAXV|>|v_max| 
  $!VARSET  |v_max|=|MAXV|
$!ENDIF

$!IF |MINV|<|v_min| 
   $!VARSET |v_min|=|MINV|
$!ENDIF


$!IF |MAXC|>|p_max| 
  $!VARSET  |p_max|=|MAXC|
$!ENDIF

$!IF |MINC|<|p_min| 
   $!VARSET |p_min|=|MINC|
$!ENDIF


$!CONTOURLEVELS NEW
  CONTOURGROUP = 1
  RAWDATA
13
-55
-45.8333333333
-36.6666666667
-27.5
-18.3333333333
-9.16666666667
0
9.16666666667
18.3333333333
27.5
36.6666666667
45.8333333333
55


$!GLOBALCONTOUR 1  LEGEND{SHOW = YES}
$!GLOBALCONTOUR 1  LEGEND{ISVERTICAL = NO}
$!GLOBALCONTOUR 1  LABELS{AUTOLEVELSKIP = 4}
$!GLOBALCONTOUR 1  LABELS{NUMFORMAT{FORMATTING = FIXEDFLOAT}}
$!GLOBALCONTOUR 1  LABELS{NUMFORMAT{PRECISION = 2}}
$!GLOBALCONTOUR 1  LABELS{NUMFORMAT{NEGATIVESUFFIX = ''}}
$!GLOBALCONTOUR 1  LABELS{NUMFORMAT{ZEROSUFFIX = ''}}
$!GLOBALCONTOUR 1  LEGEND{XYPOS{X = 85}}



$!VARSET |NZONE|=|NUMZONES|


$!FRAMELAYOUT WIDTH = 4.5
$!FRAMELAYOUT HEIGHT = 4
$!PICK ADDALL
  SELECTFRAMES = YES
$!PICK COPY
$!PICK PASTE
$!PICK PASTE
$!PICK PASTE



$!VARSET |UMIN|=-0.5
$!VARSET |UMAX|=0.5

$!VARSET |VMIN|=-0.5
$!VARSET |VMAX|=0.5


$!VARSET |PMIN|=-55
$!VARSET |PMAX|=55

#=====================================
# Y-velocity
#=====================================
$!FRAMELAYOUT XYPOS{X = 5.5}
$!FRAMELAYOUT XYPOS{Y = 4.25}
$!FIELDLAYERS SHOWVECTOR = NO
$!PLOTTYPE = CARTESIAN3D

$!THREEDAXIS ZDETAIL{VARNUM = 4}

$!IF |SHOW_MAX_MIN|==1
$!ATTACHTEXT 
  ANCHORPOS
    {
    X = 1.719621033447686
    Y = 1.846590909090907
    }
  TEXT = 'v<sub>max</sub> = |v_max| v<sub>min</sub> = |v_min|'
  SCOPE = LOCAL
$!ENDIF

$!CREATERECTANGULARZONE 
  IMAX = 2
  JMAX = 2
  KMAX = 2
  X1 = |MINX|
  Y1 = |MINZ|
  Z1 = |VMIN|
  X2 = |MAXX|
  Y2 = |MAXY|
  Z2 = |VMAX|
  XVAR = 1
  YVAR = 2
  ZVAR = 4


$!VARSET |VBOX_ZONE|=|NUMZONES|

$!FIELDLAYERS SHOWCONTOUR = NO
#$!FIELDLAYERS SHOWEDGE = NO
$!FIELDLAYERS SHOWSHADE = YES
$!ACTIVEFIELDMAPS += [|VBOX_ZONE|]
$!ACTIVEFIELDMAPS -= [1-|NZONE|]
$!RESET3DSCALEFACTORS 
$!VIEW FIT
$!ACTIVEFIELDMAPS += [1-|NZONE|]
$!ACTIVEFIELDMAPS -= [|VBOX_ZONE|]


#=====================================
# Pressure
#=====================================
$!FRAMECONTROL PUSHTOP
$!FRAMELAYOUT XYPOS{X = 5.5}
$!FIELDLAYERS SHOWVECTOR = NO
$!PLOTTYPE = CARTESIAN3D

$!CREATERECTANGULARZONE 
  IMAX = 2
  JMAX = 2
  KMAX = 2
  X1 = |MINX|
  Y1 = |MINZ|
  Z1 = |PMIN|
  X2 = |MAXX|
  Y2 = |MAXY|
  Z2 = |PMAX|
  XVAR = 1
  YVAR = 2
  ZVAR = 5
$!VARSET |PBOX_ZONE|=|NUMZONES|

$!FIELDLAYERS SHOWCONTOUR = NO
$!FIELDLAYERS SHOWSHADE = YES
$!THREEDAXIS ZDETAIL{VARNUM = 5}
$!ACTIVEFIELDMAPS -= [1-|NZONE|]
$!ACTIVEFIELDMAPS += [|PBOX_ZONE|]
$!RESET3DSCALEFACTORS 
$!THREEDAXIS AXISMODE = XYZDEPENDENT
$!THREEDAXIS DEPXTOZRATIO = 4
$!VIEW FIT
$!ACTIVEFIELDMAPS += [1-|NZONE|]
$!ACTIVEFIELDMAPS -= [|PBOX_ZONE|]

$!THREEDAXIS AXISMODE = XYDEPENDENT
$!GLOBALTHREED AXISSCALEFACT{Z = 0.02}
$!VIEW FIT

$!IF |SHOW_MAX_MIN|==1
$!ATTACHTEXT 
  ANCHORPOS
    {
    X = 1.719621033447686
    Y = 1.846590909090907
    }
  TEXT = 'p<sub>max</sub> = |p_max| p<sub>min</sub> = |p_min|'
  SCOPE = LOCAL
$!ENDIF


#=====================================
# X-velocity
#=====================================
$!FRAMECONTROL PUSHTOP
$!FRAMELAYOUT XYPOS{Y = 4.25}
$!FIELDLAYERS SHOWVECTOR = NO
$!PLOTTYPE = CARTESIAN3D

$!CREATERECTANGULARZONE 
  IMAX = 2
  JMAX = 2
  KMAX = 2
  X1 = |MINX|
  Y1 = |MINZ|
  Z1 = |UMIN|
  X2 = |MAXX|
  Y2 = |MAXY|
  Z2 = |UMAX|
  XVAR = 1
  YVAR = 2
  ZVAR = 3
$!VARSET |UBOX_ZONE|=|NUMZONES|

$!FIELDLAYERS SHOWCONTOUR = NO
$!FIELDLAYERS SHOWSHADE = YES
$!ACTIVEFIELDMAPS -= [1-|NZONE|]
$!ACTIVEFIELDMAPS += [|UBOX_ZONE|]
$!RESET3DSCALEFACTORS 
$!VIEW FIT
$!ACTIVEFIELDMAPS += [1-|NZONE|]
$!ACTIVEFIELDMAPS -= [|UBOX_ZONE|]


$!IF |SHOW_MAX_MIN|==1
$!ATTACHTEXT 
  ANCHORPOS
    {
    X = 1.719621033447686
    Y = 1.846590909090907
    }
  TEXT = 'u<sub>max</sub> = |u_max| u<sub>min</sub> = |u_min|'
  SCOPE = LOCAL
$!ENDIF

#========================================================
# Adjust velocity field plot and add drag
#========================================================
$!FRAMECONTROL PUSHTOP
$!STREAMTRACE RESETDELTATIME
$!STREAMTRACELAYERS SHOW = YES







$!FIELDMAP [1-|NUMZONES|]  VECTOR{SHOW = NO}


$!DRAWGRAPHICS TRUE
$!REDRAWALL




$!IF |PNG|==1


        $!EXPORTSETUP EXPORTFORMAT = PNG
        $!EXPORTSETUP IMAGEWIDTH = 750
        $!EXPORTSETUP EXPORTFNAME = 'moving_block|LOOP|.png'
        $!EXPORT
          EXPORTREGION = ALLFRAMES

$!ELSE

        $!IF |LOOP|>1
                $!EXPORTNEXTFRAME
        $!ELSE

                $!EXPORTSETUP
                  EXPORTREGION = ALLFRAMES
                  EXPORTFORMAT = AVI
                  EXPORTFNAME = "big_moving_block.avi"
                $!EXPORTSETUP IMAGEWIDTH = 750
                $!EXPORTSTART
        $!ENDIF

$!ENDIF

$!ENDLOOP


$!IF |PNG|==0
        $!EXPORTFINISH
$!ENDIF
